locals {
  prefix_hub         = "hub"
  hub_location       = "westeurope"
  hub_resource_group = "hub_vnet_rg"
  shared_key         = "4_v3ry_53cr37_1p53c_5h4r3d_k3y"
  hub_vnet_name      = "makis-hub-vnet"
  hub_vnet_cidr      = "10.0.0.0/16"
  gateway_subnet     = "10.0.255.224/27"
  firewall_subnet    = "10.0.0.0/24"
}

resource "azurerm_resource_group" "hub_vnet_rg" {
  name     = local.hub_resource_group
  location = local.hub_location
}

resource "azurerm_virtual_network" "hub_vnet" {
  name                = "${local.prefix_hub}_vnet"
  location            = azurerm_resource_group.hub_vnet_rg.location
  resource_group_name = azurerm_resource_group.hub_vnet_rg.name
  address_space       = [local.hub_vnet_cidr]

  tags = {
    environment = "hub_spoke"
  }
}

# Gateway Subnet
resource "azurerm_subnet" "hub_gateway_subnet" {
  name                 = "GatewaySubnet"
  resource_group_name  = azurerm_resource_group.hub_vnet_rg.name
  virtual_network_name = azurerm_virtual_network.hub_vnet.name
  address_prefixes     = [local.gateway_subnet]
}

# Firewall Subnet
resource "azurerm_subnet" "hub_firewall_subnet" {
  name                 = "AzureFirewallSubnet"
  resource_group_name  = azurerm_resource_group.hub_vnet_rg.name
  virtual_network_name = azurerm_virtual_network.hub_vnet.name
  address_prefixes     = [local.firewall_subnet]
}

# Virtual Network Gateway - ExpressRoute
resource "azurerm_public_ip" "hub_expressroute_gateway1_pip" {
  name                = "hub_expressroute_gateway1_pip"
  location            = azurerm_resource_group.hub_vnet_rg.location
  resource_group_name = azurerm_resource_group.hub_vnet_rg.name

  allocation_method = "Static"
  sku = "Standard"
}

resource "azurerm_virtual_network_gateway" "hub_expressroute_gateway" {
  name                = "hub_express_gateway1"
  location            = azurerm_resource_group.hub_vnet_rg.location
  resource_group_name = azurerm_resource_group.hub_vnet_rg.name

  type = "ExpressRoute"

  active_active = false
  enable_bgp    = false
  sku           = "ErGw1AZ"

  ip_configuration {
    name                          = "vnetGatewayConfig"
    public_ip_address_id          = azurerm_public_ip.hub_expressroute_gateway1_pip.id
    private_ip_address_allocation = "Dynamic"
    subnet_id                     = azurerm_subnet.hub_gateway_subnet.id
  }
  depends_on = [azurerm_public_ip.hub_vpn_gateway1_pip]
}
# Virtual Network Gateway - VPN
resource "azurerm_public_ip" "hub_vpn_gateway1_pip" {
  name                = "hub_vpn_gateway1_pip"
  location            = azurerm_resource_group.hub_vnet_rg.location
  resource_group_name = azurerm_resource_group.hub_vnet_rg.name

  allocation_method = "Static"
  sku = "Standard"
}

resource "azurerm_virtual_network_gateway" "hub_vnet_gateway" {
  name                = "hub_vpn_gateway1"
  location            = azurerm_resource_group.hub_vnet_rg.location
  resource_group_name = azurerm_resource_group.hub_vnet_rg.name

  type     = "Vpn"
  vpn_type = "RouteBased"

  active_active = false
  enable_bgp    = false
  sku           = "VpnGw1"

  ip_configuration {
    name                          = "vnetGatewayConfig"
    public_ip_address_id          = azurerm_public_ip.hub_vpn_gateway1_pip.id
    private_ip_address_allocation = "Dynamic"
    subnet_id                     = azurerm_subnet.hub_gateway_subnet.id
  }
  depends_on = [azurerm_public_ip.hub_vpn_gateway1_pip, azurerm_virtual_network_gateway.hub_expressroute_gateway]
}

# Azure Firewall 
resource "azurerm_public_ip" "hub_firewall_pip" {
  name                = "hub_firewall_pip"
  location            = azurerm_resource_group.hub_vnet_rg.location
  resource_group_name = azurerm_resource_group.hub_vnet_rg.name

  allocation_method = "Static"
  sku = "Standard"
}

resource "azurerm_firewall" "azure_firewall" {
  name                = "azure_firewall"
  location            = azurerm_resource_group.hub_vnet_rg.location
  resource_group_name = azurerm_resource_group.hub_vnet_rg.name

  sku_name = "AZFW_VNet"
  sku_tier = "Standard"

  ip_configuration {
    name                 = "configuration"
    subnet_id            = azurerm_subnet.hub_firewall_subnet.id
    public_ip_address_id = azurerm_public_ip.hub_firewall_pip.id
  }
}
